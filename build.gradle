apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

dependencies {
    compile gradleApi()
    groovy localGroovy()
}


repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.ant:ant:1.8.4'
    compile 'org.antlr:antlr-runtime:3.0.1'
    testCompile 'junit:junit:4.11'
}

group = 'org.inkinen'
version = '0.1-SNAPSHOT'

compileJava {
    options.encoding = 'UTF-8'
    //options.compilerArgs = ['-Xlint', '-Xlint:-serial', '-Xlint:-path', '-Xlint:deprecation', '-Xlint:unchecked']
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (System.getProperty('releaseLocal').equals('true')) {
                repository(url: '../repo')
            } else if (System.getProperty('releaseUpload').equals('true') && version.endsWith('-SNAPSHOT')) {
                // Only upload when we really mean to, i.e. specify releaseUpload=true
                repository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    // Credentials need to go to ~/.gradle/gradle.properties
                    // There it is possible to define the variables, which are being used here
                    authentication(userName: mavenUser, password: mavenPassword)
                }
            }
            pom.project {
                description 'Preprocessor plugin for gradle. Uses the antenna preprocesor, without needing a WTK installation to run the processor'
                url 'https://github.com/kaiinkinen/gradle-preprocessor-plugin.git'
                scm {
                    url 'scm:https://github.com/kaiinkinen/gradle-preprocessor-plugin.git'
                    connection 'scm:https://github.com/kaiinkinen/gradle-preprocessor-plugin.git'
                    developerConnection 'scm:https://github.com/kaiinkinen/gradle-preprocessor-plugin.git'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'kaiinkinen'
                        name 'Kai Inkinen'
                    }
                }
            }
        }
    }
}
